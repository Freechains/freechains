#ifndef _CLIENT_CEU
#define _CLIENT_CEU

class Message00_prepare with
    var Message& msg;
    function (Message& msg)=>Message00_prepare build;
do
    function (Message& msg)=>Message00_prepare build do
        this.msg = &msg;
    end

    // => MESSAGE
    do
        var int major = [[ MSG.id_t.major ]];
        var int minor = [[ MSG.id_t.minor ]];
        this.msg.bytes = []..this.msg.bytes..
                         [MESSAGE_MAGIC_0, MESSAGE_MAGIC_1, major, minor];
    end

    // => CHAIN_SIGNED?
    // => CHAIN_LEN
    // => CHAIN_DATA
    do
        var bool chain_signed = [[ MSG.chain_signed ]];
        var int  chain_len    = [[ MSG.chain_len    ]];
        _assert(chain_len>=0 and chain_len<256);
        var u8 chain = (chain_len | (chain_signed<<7));
        this.msg.bytes = []..this.msg.bytes..[chain];

        if chain_len > 0 then
            var char[] chain_data = [[ MSG.chain_data ]];
            this.msg.bytes = []..this.msg.bytes..chain_data;
        end
    end

    // => PAYLOAD_HASH
    do
        loop i in HASH_BYTES do
            if i==0 or i==HASH_BYTES-1 then
                this.msg.bytes = []..this.msg.bytes..[0xEE];  // TODO
            else
                this.msg.bytes = []..this.msg.bytes..[0xFF];  // TODO
            end
        end
    end


    this.msg.offs = []..this.msg.offs..[$this.msg.bytes];

    //////////////////////////////////////////////////////////////////////////

    // => PoW
    do
        var u32 pow = 0xEEFFFFEE;       // TODO
        PUT_NETWORK_U32(this.msg.bytes, pow);
    end

    // => PoS
    do
        var u8 pos = 0xEE;              // TODO
        this.msg.bytes = []..this.msg.bytes..[pos];
    end

    // => BACK_HASH
    do
        loop i in HASH_BYTES do
            if i==0 or i==HASH_BYTES-1 then
                this.msg.bytes = []..this.msg.bytes..[0xEE];  // TODO
            else
                this.msg.bytes = []..this.msg.bytes..[0xFF];  // TODO
            end
        end
    end

    // => TAIL_HASH
    do
        loop i in HASH_BYTES do
            if i==0 or i==HASH_BYTES-1 then
                this.msg.bytes = []..this.msg.bytes..[0xEE];  // TODO
            else
                this.msg.bytes = []..this.msg.bytes..[0xFF];  // TODO
            end
        end
    end

    // => PAYLOAD_LEN
    // => PAYLOAD
    var char[] payload = [[ MSG.payload ]];
    PUT_NETWORK_U32(this.msg.bytes, $payload);
    this.msg.bytes = []..this.msg.bytes..payload;

    this.msg.offs = []..this.msg.offs..[$this.msg.bytes];

    escape _ERR_NONE;
end

interface PeerListener with
    event int ok;
end

class PeerSend with
input:
    var int peer_i;
    var Message&      msg;
    var PeerListener& listener;

    function (int i, Message& msg, PeerListener& listener)=>PeerSend build;
do
    function (int i, Message& msg, PeerListener& listener)=>PeerSend build do
        this.peer_i   = i;
        this.msg      = &msg;
        this.listener = &listener;
    end

    var char[] ip   = [[ APP.client.peers[@this.peer_i][1] ]];
    var int    port = [[ APP.client.peers[@this.peer_i][2] ]];
    _printf("[client] Connecting to %s:%d.\n", (_char&&)&&ip, port);
    var UV_TCP_Client c = UV_TCP_Client.build(&ip, port);
    var int status = await c.ok;

    if status == 0 then
        _printf("[client] Connected to %s:%d.\n", (_char&&)&&ip, port);
        watching c do
            var u32 starting = 0;
            loop i in $this.msg.offs do
                // => offs[i]
                var u32 excluding = this.msg.offs[i];
                do UV_Stream_Write.build2(&_UV_STREAM_ALIAS(c.tcp),
                                          &this.msg.bytes,
                                          starting, excluding);

                // <= OK?
                var int ok = 0;
                do
                    var byte[] recv;
                    var UV_Stream_Read r = UV_Stream_Read.build(&_UV_STREAM_ALIAS(c.tcp),&recv);
                    AWAIT_UNTIL(r,1);
                    _assert(r.next >= 1);
                    if r.next > 1 then
                        escape _ERR_EXTRA_BYTES;
                    end
                    ok = recv[0];
                end
                starting = excluding;
            end
        end
        escape _ERR_NONE;
    end

    _printf("[client] Diconnected from %s:%d.\n", (_char&&)&&ip, port);
    escape _ERR_DISCONNECTED;
end

class PeersSend with
    interface PeerListener;
input:
    var Message& msg;

    function (Message& msg)=>PeersSend run;
do
    function (Message& msg)=>PeersSend run do
        this.msg = &msg;
    end

    _printf("[client] Broadcasting message...\n");
    var int n = [[#APP.client.peers]];
    loop i in n do
        spawn PeerSend.build(i+1, &this.msg, &this);
    end
    loop i in n do
        var int err = await this.ok;
        _printf("[client]\tpeer %d\n", err);
    end
    _printf("[client] Broadcast OK!\n");

    escape _ERR_NONE;
end

class Client with
do
    var int ni = [[ #APP.messages ]];

    loop i in ni do
        [[ MSG = APP.messages[@i+1] ]];
        var Message msg;
        var bool is_00 = [[ MSG.id=='0.0' ]];
        if is_00 then
            var int err = do Message00_prepare.build(&msg);
            _assert(err == _ERR_NONE);
        else
            _assert(0);  // message not supported
        end
        do PeersSend.run(&msg);
    end

    escape _ERR_NONE;
end

#endif
