#ifndef _MESSAGE70_CEU
#define _MESSAGE70_CEU

/*
 * CRYPTO:
 *
 *  msg = {
 *      create     = <string>, -- 'shared' or 'public-private'
 *      passphrase = <string>,
 *  }
 *
 *  ret = {
 *      public  = <string>,
 *      private = <string>,
 *  }
 */

// echo -n -e "PS\x07\x00\x00\x00\x00\x0F{passphrase=''}" | nc localhost 8500

// => LEN
await UV_Stream_Read_N(&outer.tcp, sizeof(u32));
var u32 len = GET_BE32TOH(outer.tcp.buffer,0);
$outer.tcp.buffer = $outer.tcp.buffer - sizeof(u32);
_dbg_tcp(2, &&outer.tcp.handle, "server", "LEN = %d", len);

// => MSG
await UV_Stream_Read_N(&outer.tcp, (len as usize));
//outer.tcp.buffer = outer.tcp.buffer..[0];
//_dbg(3, "server | MESSAGE 70 | %s", &&outer.tcp.buffer[0]);
[[
    assert(load('msg = '..@outer.tcp.buffer))()
    print('>>>', FC.tostring(msg,'plain'))
    assert(msg.create)
]]

do
    var[] byte passphrase = []..[[ assert(msg.passphrase, 'TODO: omit passphrase / random key') ]];

    var bool is_shared = [[ msg.create == 'shared' ]];
    if is_shared then
        var[{crypto_generichash_KEYBYTES}] byte ret;
        _ceu_vector_setlen(&&ret, {crypto_generichash_KEYBYTES}, 1);
        _ceu_assert(_sodium_passphrase2pwhash(&&ret[0], {crypto_generichash_KEYBYTES}, &&passphrase[0], $passphrase) == 0, "bug found");
        [[
            RET = FC.hash2hex(@ret)
        ]]
    else
        var[{crypto_sign_PUBLICKEYBYTES}] byte pk;
        var[{crypto_sign_SECRETKEYBYTES}] byte sk;
        _ceu_vector_setlen(&&pk, {crypto_sign_PUBLICKEYBYTES}, 1);
        _ceu_vector_setlen(&&sk, {crypto_sign_SECRETKEYBYTES}, 1);

        _ceu_assert(_sodium_passphrase2keys(&&passphrase[0], $passphrase, &&pk[0], &&sk[0]) == 0, "bug found");

        [[
            RET = {
                public  = FC.hash2hex(@pk),
                private = FC.hash2hex(@sk),
            }
        ]]
    end
end

// <= MSG
[[ print('RET', FC.tostring(RET,'plain')) ]];
outer.tcp.buffer = []..[[ FC.tostring(RET,'plain') ]];
await UV_Stream_Write_N(&outer.tcp,_);

#endif
