#ifndef _SERVER_CEU
#define _SERVER_CEU

native do
    enum {
        MESSAGE10_HAVE = 1, // [ok ACK] server already has it
        MESSAGE10_NOTSUB,   // [no ACK] server is not subscribed to its chain
        MESSAGE10_BACK_HASH_JUMP_LIMIT, // TODO:
        MESSAGE10_PAYLOAD_LEN_LIMIT,    // TODO: move PAYLOAD_LEN to header
    };
end

class Message10_receive with
input:
    var _uv_tcp_t& tcp;
    var Message&   msg;

    function (_uv_tcp_t& tcp, Message& msg)=>Message10_receive build;
do
    function (_uv_tcp_t& tcp, Message& msg)=>Message10_receive build do
        this.tcp = &tcp;
        this.msg = &msg;
    end

    [[
        MSG = {
            id = '1.0',
            chain = {
                key    = nil,
                zeros  = nil,
            },
            block_hash = nil,
            back_hash  = nil,
            tail_hash  = nil,
        }
    ]];

    var UV_Stream_Read r = UV_Stream_Read.build(&_UV_STREAM_ALIAS(this.tcp),
                                                &this.msg.bytes);

    var int nxt = 0;
    var int off = 0;

    // <= MESSAGE
    // TODO: with other message, move to outside the class
    do
        // [check] Is this message '1.0'? [no ACK]

        off = nxt;
        nxt = nxt + 1+1+1+1;
        AWAIT_UNTIL(r,nxt);

        if this.msg.bytes[off+0] != MESSAGE_MAGIC_0  or
           this.msg.bytes[off+1] != MESSAGE_MAGIC_1  or
           this.msg.bytes[off+2] != 1                or
           this.msg.bytes[off+3] != 0
        then
            escape _ERR_MESSAGE;
        end
        _printf("[server] MESSAGE = [%d %d %d %d]\n",
                this.msg.bytes[off+0],
                this.msg.bytes[off+1],
                this.msg.bytes[off+2],
                this.msg.bytes[off+3]);
    end

    // <= CHAIN_SIGNED?
    // <= CHAIN_LEN
    // <= CHAIN_KEY
    var char[] chain_key;
    var bool chain_signed = false;
    do
        off = nxt;
        nxt = nxt + 1;
        AWAIT_UNTIL(r,nxt);
        var u8 v = this.msg.bytes[off];
        chain_signed = (v>>7);
        var int chain_len = (v&0x7F);
        _printf("[server] CHAIN_SIGNED = %d\n", chain_signed);
        _printf("[server] CHAIN_LEN    = %d\n", chain_len);

        off = nxt;
        nxt = nxt + chain_len;
        AWAIT_UNTIL(r,nxt);
        $chain_key := chain_len;
        _strncpy((_char&&)&&chain_key, (_char&&)&&this.msg.bytes[off], chain_len);
        _printf("[server] CHAIN_KEY    = %s\n", (_char&&)&&chain_key);

        [[ MSG.chain.key = @chain_key ]];
    end

    // <= BLOCK_HASH
    var byte[HASH_BYTES] block_hash;
    do
        off = nxt;
        nxt = nxt + HASH_BYTES;
        AWAIT_UNTIL(r,nxt);
        $block_hash := HASH_BYTES;
        _ceu_vector_copy_buffer(&&block_hash, 0,
                                (_byte&&)&&this.msg.bytes[off], HASH_BYTES);
        [[
            MSG.block_hash = @block_hash
            print('[server] BLOCK_HASH = {')
            print(hex_dump(MSG.block_hash))
            print('}')
        ]];
    end
    _assert($block_hash == HASH_BYTES);

    // == END
    _assert(r.next >= nxt);
    if r.next > nxt then
        escape _ERR_EXTRA_BYTES;
    end

    // == CHECK
    // => ACK
    do
        // [check] Do I have this message hash? [ok ACK]

        var bool have = [[ APP.blocks[@block_hash] ]];
        [[ APP.blocks[@block_hash] = true ]];

        // [check] Am I subscribed to the message chain? [no ACK]
        // [check] Does the message has the required zeros? [no ACK]

        if not have then
            [[ CHAIN = APP.chains[@chain_key] ]];
            var bool is_sub = [[ CHAIN ~= nil ]];
            if is_sub then
                var int zeros = _leading_zeros((_byte&&)&&block_hash, HASH_BYTES);
                is_sub = [[ CHAIN.zeros <= @zeros ]];
                [[ MSG.chain.zeros = @zeros ]];
            end
            if not is_sub then
                escape _MESSAGE10_NOTSUB;   // no ACK
            end
        end

        var byte[1] send = [not have];
        this.msg.offs = []..this.msg.offs..[nxt];
        do UV_Stream_Write.build(&_UV_STREAM_ALIAS(this.tcp), &send);

        if have then
            escape _MESSAGE10_HAVE;
        end
    end

    //////////////////////////////////////////////////////////////////////////

    // <= PoW
    do
        off = nxt;
        nxt = nxt + sizeof(u32);
        AWAIT_UNTIL(r,nxt);
        var u32 pow = GET_HOST_U32(this.msg.bytes,off);
        _printf("[server] POW = %X\n", pow);
        [[ MSG.pow = @pow ]];
    end

    // <= PoS
    // TODO: check
    do
        off = nxt;
        nxt = nxt + 1;
        AWAIT_UNTIL(r,nxt);
        var int pos = this.msg.bytes[off];
        _printf("[server] POS = %X\n", pos);
        [[ MSG.pos = @pos ]];
    end

    // <= BACK_HASH
    var byte[HASH_BYTES] back_hash;
    do
        off = nxt;
        nxt = nxt + HASH_BYTES;
        AWAIT_UNTIL(r,nxt);
        $back_hash := HASH_BYTES;
        _ceu_vector_copy_buffer(&&back_hash, 0,
                                (_byte&&)&&this.msg.bytes[off], HASH_BYTES);
        [[
            MSG.back_hash = @back_hash
            print('[server] BACK_HASH = {')
            print(hex_dump(MSG.back_hash))
            print('}')
        ]];

        // [check] BACK_HASH is young enough? [no ACK]
        do
#if 0
            var int n = [[ APP.chains.BACK_HASH_JUMP_LIMIT ]];
            [[ HEAD = APP.chains[MSG.chain.key].heads[MSG.chain.zeros] ]];
            loop i in n do
                var bool is_eq = [[ HEAD == MSG.back_hash ]];
                if is_eq then
                    xxx
                end
               [[ HEAD = APP ]]
            end
#endif
        end
    end
    _assert($back_hash == HASH_BYTES);

    // <= TAIL_HASH
    var byte[HASH_BYTES] tail_hash;
    do
        off = nxt;
        nxt = nxt + HASH_BYTES;
        AWAIT_UNTIL(r,nxt);
        $tail_hash := HASH_BYTES;
        _ceu_vector_copy_buffer(&&tail_hash, 0,
                                (_byte&&)&&this.msg.bytes[off], HASH_BYTES);
        [[
            MSG.tail_hash = @tail_hash
            print('[server] TAIL_HASH = {')
            print(hex_dump(MSG.tail_hash))
            print('}')
        ]];
    end
    _assert($tail_hash == HASH_BYTES);

    // <= PAYLOAD_LEN
    // <= PAYLOAD
    do
        off = nxt;
        nxt = nxt + sizeof(u32);
        AWAIT_UNTIL(r,nxt);
        var u32 payload_len = GET_HOST_U32(this.msg.bytes,off);
        _printf("[server] PAYLOAD_LEN = %d\n", payload_len);

        off = nxt;
        nxt = nxt + payload_len;
        AWAIT_UNTIL(r,nxt);

        var byte[] payload;
        $payload := payload_len;
        _ceu_vector_copy_buffer(&&payload, 0,
                                &&(((_byte&&)&&this.msg.bytes)[off]),
                                payload_len);
        [[
            MSG.payload = @payload
            print('[server] PAYLOAD = '..MSG.payload)
        ]];
    end

    // == END
    _assert(r.next >= nxt);
    if r.next > nxt then
        escape _ERR_EXTRA_BYTES;
    end

    this.msg.offs = []..this.msg.offs..[nxt];

    escape _ERR_NONE;
end

class Message10_link with
input:
    var Message& msg;

    function (Message& msg)=>Message10_link build;
do
    function (Message& msg)=>Message10_link build do
        this.msg = &msg;
    end

    // == CHECK
    do
        [[ HEAD = APP.chains[MSG.chain.key].heads[MSG.chain.zeros] ]];
        var bool same = [[ HEAD == MSG.block_hash ]];
    end
end

class UV_TCP_Server_Handler with
    var _uv_tcp_t& tcp;
    function (_uv_tcp_t& tcp)=>UV_TCP_Server_Handler build;
do
    function (_uv_tcp_t& tcp)=>UV_TCP_Server_Handler build do
        this.tcp = &tcp;
    end

    var Message msg;
    var int ret = do Message10_receive.build(&this.tcp, &msg);
    _assert(ret >= _ERR_NONE);
    _printf("[server] ret = %d\n", ret);
    if ret==_MESSAGE10_HAVE then
        _assert($msg.offs == 1);
    else/if ret==_MESSAGE10_NOTSUB then
        _assert($msg.offs == 0);
    else
        _assert(ret == _ERR_NONE);
        _assert($msg.offs == 2);
        var int err = do Message10_link.build(&msg);
        if err == _ERR_NONE then
            err = do PeersSend.run(&msg);
            _assert(err == _ERR_NONE);
        end
    end
end

#endif
