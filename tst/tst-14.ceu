#include "common.ceu"

///////////////////////////////////////////////////////////////////////////////
// 0->1<-2
//  - 0 sends 0.1
//  - 0 sends 0.2
//  - ...     0.9
//  - 2 sends 2.1,2.2,...,2.8
//  - (2.1,...,2.8) -> /no/ 0.1 -> ... -> 0.9 -> ''
///////////////////////////////////////////////////////////////////////////////

event none e;

par/or do

///////////////////////////////////////////////////////////////////////////////
// 0
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        var UV_TCP_Server_Data shared = _;
        [[
            dofile 'src/common.lua'
            CFG = {
                chains = {
                    zeros_raise = TODO,     -- global and per-chain
                    [1] = {
                        key   = '',
                        zeros = 0,
                        peers = {
                            { address='127.0.0.1', port=8331 },
                        },
                    },
                },
            }
        ]];

        await Init();
        await 100ms;

        var int i;
        loop i in [1 -> 9] do
            [[
                ARGS = {
                    message = {
                        version = '1.0',
                        chain = {
                            key   = '',
                            zeros = 0,
                        },
                        payload = '0.'..@i
                    }
                }
            ]];
            await Client(&shared);
        end
        emit e;
        await FOREVER;
    end

with

///////////////////////////////////////////////////////////////////////////////
// 1
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        var UV_TCP_Server_Data shared = _;
        [[
            dofile 'src/common.lua'
            CFG = {
                server = {
                    address = '127.0.0.1',
                    port    = '8331',
                    backlog = 128,
                },
                chains = {
                    [1] = {
                        key   = '',
                        zeros = 0,
                    },
                },
            }
        ]];

        await Init();

        var[] byte ip      = [] .. [[ CFG.server.address ]] .. [0];
        var   int  port    = [[ CFG.server.port ]];
        var   int  backlog = [[ CFG.server.backlog ]];
        _dbg(0, "server | listening in %s:%d", &&ip[0], port);
        spawn UV_TCP_Server(&&ip[0], port, backlog, _, &shared);

        var int i;
        loop i in [1 -> 10] do
            await shared.ok;
        end
        [[
            CHAIN1 = FC.chain_flatten('||0|')
            print'===> SERVER'
            print('>'..string.rep('=',78))
            print(FC.chain_tostring('||0|'))
            print('<'..string.rep('=',78))
        ]];
        [=[
            assert( #CHAIN1==10 and
                    CHAIN1[2].publication.payload==[[0.1]] and
                    CHAIN1[10].publication.payload==[[0.9]])
            print'OK!'
        ]=];
    end

with

///////////////////////////////////////////////////////////////////////////////
// 2
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        var UV_TCP_Server_Data shared = _;
        [[
            dofile 'src/common.lua'
            CFG = {
                chains = {
                    zeros_raise = TODO,     -- global and per-chain
                    [1] = {
                        key   = '',
                        zeros = 0,
                        peers = {
                            { address='127.0.0.1', port=8331 },
                        },
                    },
                },
            }
        ]];

        await Init();
        await e;

        var int i;
        loop i in [1 -> 8] do
            [[
                ARGS = {
                    message = {
                        version = '1.0',
                        chain = {
                            key   = '',
                            zeros = 0,
                        },
                        payload = '2.'..@i
                    }
                }
            ]];
        end
        await Client(&shared);
        await FOREVER;
    end

end

escape 0;
