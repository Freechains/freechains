#define DETERMINISTIC
#include "common.ceu"

_srand(_time(null));

///////////////////////////////////////////////////////////////////////////////
// 0->1->...->N->0
///////////////////////////////////////////////////////////////////////////////

#define N 10

code/await Node (var int i) -> none
    throws Exception.Lua
do
    lua[] do
        var int local  = 8340+i;
        var int remote = 8341+i;
        if i == (N-1) then
            remote = 8340;
        end

        [[
            dofile 'src/common.lua'
            CFG = {
                daemon = {
                    address = '127.0.0.1',
                    port    = @local,
                },
                no_persistency = true,
                chains = {
                    ['tst-31'] = {
                        key   = 'tst-31',
                        zeros = 0,
                        peers = {
                            { address='127.0.0.1', port=@remote },
                        },
                    },
                },
            }
        ]]

        var& Init init = spawn Init();
        init.shared.n = i;
        await init.ok;

        spawn do
            every 5s do
                [[ print('>>>', @local, FC.chains['tst-31'][0].n) ]];
            end
        end

        await 100ms;

        do
#if 0
            spawn do
                var int  a;
                var bool err;
                var int  c;
                every (a,err,c) in init.shared.ok do
#if 0
[[
print(@i, '>'..string.rep('=',70))
print(GG.chain_tostring('|tst-31|0|'))
print(@i, '<'..string.rep('=',70))
]];
#endif

                    if err then
                        _printf("server | err | %d %d %d\n", a,err,c);
                    end
                end
            end
#endif

            var int j;
            loop j in [0 -> N[ do
                // how much time between each message?
                await (_rand() % 100) ms;
                [[
                    ARGS = {
                        message = {
                            version = '1.0',
                            chain = {
                                key   = 'tst-31',
                                zeros = 0,
                            },
                            payload = string.format('%03d',@i)..'.'..string.format('%03d',@j),
                        }
                    }
                ]];
                await Client_10(&init.shared);
//[[print(@i,@j, "clients") ]];
            end
//[[print(@i, "CLIENTS") ]];

            // wait all messages to be received
            AWAIT_CHAIN_N(init.shared, 'tst-31', 0, N*N+1);

            // wait for consensus
            loop do
                watching init.shared.ok_server do
                    await (N)s;
//_dbg(0, "server | shared.ok | 0.0.0.0:%d", port);
                    [[ print('>>>', @local, 'STOP', FC.chains['tst-31'][0].n) ]];
                    break;
                end
            end
        end

#if 0
[[
print(@i, '>'..string.rep('=',70))
print(GG.chain_tostring('|tst-31|0|'))
print(@i, '<'..string.rep('=',70))
]];
//[[print(@i, "END") ]];

        [[ ARGS = { chain=FC.chains['tst-31'][0] } ]];
        await FS_write_10();
#endif
        [[
            FC.dot( FC.chains['tst-31'][0].head, '/tmp/tst-31-'..@i..'.dot' )
        ]]
    end
end

pool[N] Node nodes;

var int i;
loop i in [0 -> N[ do
    spawn Node(i) in nodes;
end

// wait all nodes to terminate
var&? Node n;
loop n in nodes do
    await n;
end

//_ceu_dbg_assert({BLOCKS_RECEIVED} == N*N);

// assert consensus
[[ tst = assert(io.open('/tmp/tst-31-0.dot')):read'*a' ]]
var int i;
loop i in [1 -> N[ do
    [[
        tmp = assert(io.open('/tmp/tst-31-'..@i..'.dot')):read'*a'
        assert(tst == tmp, 'differ')
    ]]
end

[[ print'OK!' ]];

escape 0;
