#include "common.ceu"

///////////////////////////////////////////////////////////////////////////////
// 1<-0->2->1
//  - close the connection "1" during last message
//      - 0.3 is not received by "1" from "0", but from "2"
///////////////////////////////////////////////////////////////////////////////

par/and do

///////////////////////////////////////////////////////////////////////////////
// 0
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        [[
            dofile 'src/common.lua'
            CFG = {
                no_persistency = true,
                zeros_raise = TODO,     -- global and per-chain
                chains = {
                    [''] = {
                        key   = '',
                        zeros = 0,
                        peers = {
                            { address='127.0.0.1', port=8341 },
                            { address='127.0.0.1', port=8342 },
                        },
                    },
                },
            }
        ]];

        var& Init init = spawn Init();
        await 100ms;

        native/plain _uv_stream_t_ptr;
        native/pre do
            typedef uv_stream_t* uv_stream_t_ptr;
        end
        code/await Disconnect (var _uv_stream_t_ptr h) -> none do
            await async (h) do
                emit UV_STREAM_ERROR(h,-_ECONNRESET);
            end
        end
        pool[1] Disconnect ds;

        var int i;
        loop i in [1 -> 3] do
            [[
                ARGS = {
                    message = {
                        version = '1.0',
                        chain = {
                            key   = '',
                            zeros = 0,
                        },
                        payload = '0.'..@i
                    }
                }
            ]];
            var&? Client c = spawn Client(&init.shared);

            // close 1st connection for 3rd message
            if i == 3 then
                // 2nd message
                var int j;
                loop j in [1 -> 2] do
                    // 4 connections
                    //  client -> server = 2
                    //  client -> server = 2
                    var _uv_connect_t&& c;
                    var int err;
                    (c,err) = await UV_STREAM_CONNECT;
                    if j == 1 then
                        spawn Disconnect(_ID(c:handle)) in ds;
                    end
                end
            end
            await c;
        end

        [[
            FC.dot( FC.chains[''][0].head, '/tmp/tst-18-00.dot' )
        ]];
    end

with

///////////////////////////////////////////////////////////////////////////////
// 1
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        [[
            dofile 'src/common.lua'
            CFG = {
                daemon = {
                    address = '127.0.0.1',
                    port    = '8341',
                    backlog = 128,
                },
                no_persistency = true,
                zeros_raise = TODO,     -- global and per-chain
                chains = {
                    [''] = {
                        key   = '',
                        zeros = 0,
                    },
                },
            }
        ]];

        var& Init init = spawn Init();
        AWAIT_CHAIN_N(init.shared, '', 0, 4);

        [[
            FC.dot( FC.chains[''][0].head, '/tmp/tst-18-01.dot' )
        ]];
    end

with

///////////////////////////////////////////////////////////////////////////////
// 2
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        [[
            dofile 'src/common.lua'
            CFG = {
                daemon = {
                    address = '127.0.0.1',
                    port    = '8342',
                    backlog = 128,
                },
                no_persistency = true,
                zeros_raise = TODO,     -- global and per-chain
                chains = {
                    [''] = {
                        key   = '',
                        zeros = 0,
                        peers = {
                            { address='127.0.0.1', port=8341 },
                        },
                    },
                },
            }
        ]];

        var& Init init = spawn Init();
        AWAIT_CHAIN_N(init.shared, '', 0, 4);
        [[
            FC.dot( FC.chains[''][0].head, '/tmp/tst-18-02.dot' )
        ]];
        await 100ms;
    end

end

[[
    local tst   = assert(io.open('tst/dots/tst-18.dot')):read'*a'
    local tmp00 = assert(io.open('/tmp/tst-18-00.dot')):read'*a'
    local tmp01 = assert(io.open('/tmp/tst-18-01.dot')):read'*a'
    local tmp02 = assert(io.open('/tmp/tst-18-02.dot')):read'*a'
    assert(tst==tmp00 and tst==tmp01 and tst==tmp02)

    print'OK!'
]]

escape 0;
