#define DETERMINISTIC
#include "common.ceu"

_srand(_time(null));

///////////////////////////////////////////////////////////////////////////////
// 0->1->...->N->0
///////////////////////////////////////////////////////////////////////////////

#define N 2

code/await Node (var int i) -> none
    throws Exception.Lua
do
    lua[] do
        var int local  = 8340+i;
        var int remote = 8341+i;
        if i == (N-1) then
            remote = 8340;
        end

        [[
            dofile 'src/common.lua'
            CFG = {
                daemon = {
                    address = '127.0.0.1',
                    port    = @local,
                },
                no_persistency = true,
                chains = {
                    ['tst-29'] = {
                        key   = 'tst-29',
                        zeros = 0,
                        peers = {
                            { address='127.0.0.1', port=@remote },
                        },
                    },
                },
            }
        ]]

        var& Init init = spawn Init();
        await 100ms;

        do
            var int j;
            loop j in [0 -> N[ do
                // how much time between each message?
                await (_rand()%100) ms;
                [[
                    ARGS = {
                        message = {
                            version = '1.0',
                            chain = {
                                key   = 'tst-29',
                                zeros = 0,
                            },
                            payload = string.format('%03d',@i)..'.'..string.format('%03d',@j),
                        }
                    }
                ]];
//[[print('>>>', @i,@j, "clients") ]];
                await Client_10(&init.shared);
//[[print('<<<', @i,@j, "clients") ]];
            end

            // wait all messages to be received
            AWAIT_CHAIN_N(init.shared, 'tst-29', 0, N*N+1);

            // wait for consensus
            loop do
                watching init.shared.ok_server do
                    await (N)s;
                    break;
                end
            end
        end

#if 0
        [[ ARGS = { chain=FC.chains['tst-29'][0] } ]];
        await FS_write_10();
#endif
        [[
            FC.dot( FC.chains['tst-29'][0].head, '/tmp/tst-29-'..@i..'.dot' )
        ]]
    end
end

pool[N] Node nodes;

var int i;
loop i in [0 -> N[ do
    spawn Node(i) in nodes;
end

// wait all nodes to terminate
var&? Node n;
loop n in nodes do
    await n;
end

//_printf("BLOCKS_RECEIVED  = %d\n", {BLOCKS_RECEIVED});
//_ceu_dbg_assert({BLOCKS_RECEIVED} == N*N);

// assert consensus
[[ tst = assert(io.open('/tmp/tst-29-0.dot')):read'*a' ]]
var int i;
loop i in [1 -> N[ do
    [[
        tmp = assert(io.open('/tmp/tst-29-'..@i..'.dot')):read'*a'
        assert(tst == tmp, 'differ')
    ]]
end

[[ print'OK!' ]];

escape 0;
