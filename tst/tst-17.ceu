#include "common.ceu"

///////////////////////////////////////////////////////////////////////////////
// 1<-0->2
//  - close the connection "2" during second message
//      - 0.2 is not received by "2", but 0.3 is, and then 0.2
///////////////////////////////////////////////////////////////////////////////

par/and do

///////////////////////////////////////////////////////////////////////////////
// 0
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        var UV_TCP_Server_Data shared = _;
        [=[
            dofile 'src/common.lua'
            dofile 'cfg/config.lua'

            CLIENT {
                peers = {
                    {
                        host = { '127.0.0.1', '8331' },
                    },
                    {
                        host = { '127.0.0.1', '8332' },
                    },
                },
            }
        ]=];

        await Init();
        await 100ms;

        native/plain _uv_stream_t_ptr;
        native/pre do
            typedef uv_stream_t* uv_stream_t_ptr;
        end
        code/await Disconnect (var _uv_stream_t_ptr h) -> none do
            await async (h) do
                emit UV_STREAM_ERROR(h,-_ECONNRESET);
            end
        end
        pool[1] Disconnect ds;


        par/and do
            // close 2nd connection for 2nd message
            await UV_STREAM_CONNECT;
            await UV_STREAM_CONNECT;
            await UV_STREAM_CONNECT;
            do
                native/plain _uv_connect_t_ptr;
                native/pre do
                    typedef uv_connect_t* uv_connect_t_ptr;
                end
                var _uv_connect_t_ptr c;
                var int err;
                (c,err) = await UV_STREAM_CONNECT;
                await async (c) do
                    emit UV_STREAM_ERROR(c:handle, -_ECONNRESET);
                end
            end
        with
            var int i;
            loop i in [1 -> 3] do

                [=[
                    APP.messages = {}
                    MESSAGE {
                        id = '1.0',
                        chain = {
                            key   = '',
                            zeros = 0,
                        },
                        payload = '0.'..@i
                    }
                ]=];
                await ClientMessages(&shared);
            end
        end
    end

with

///////////////////////////////////////////////////////////////////////////////
// 1
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        var UV_TCP_Server_Data shared = _;
        [[
            dofile 'src/common.lua'
            dofile 'cfg/config.lua'
            SERVER {
                host = { '127.0.0.1', '8331' },
            }
        ]];

        await Init();

        var[] byte ip      = [] .. [[ APP.server.host[1] ]] .. [0];
        var   int  port    = [[ APP.server.host[2] ]];
        var   int  backlog = [[ APP.server.backlog ]];
        _dbg(0, "server | listening in %s:%d", &&ip[0], port);
        spawn UV_TCP_Server(&&ip[0], port, backlog, _, &shared);

        await shared.ok;
        await shared.ok;
        await shared.ok;
        [[
            CHAIN1 = GG.chain_flatten('||0|')
            print'===> SERVER'
            print('>'..string.rep('=',78))
            print(GG.chain_tostring('||0|'))
            print('<'..string.rep('=',78))
        ]];
        [=[
            assert( #CHAIN1==4 and
                    CHAIN1[4].txs[1]==[[0.3]])
            print'OK 1/2!'
        ]=];
    end

with

///////////////////////////////////////////////////////////////////////////////
// 2
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        var UV_TCP_Server_Data shared = _;
        [[
            dofile 'src/common.lua'
            dofile 'cfg/config.lua'
            SERVER {
                host = { '127.0.0.1', '8332' },
            }
        ]];

        await Init();

        var[] byte ip      = [] .. [[ APP.server.host[1] ]] .. [0];
        var   int  port    = [[ APP.server.host[2] ]];
        var   int  backlog = [[ APP.server.backlog ]];
        _dbg(0, "server | listening in %s:%d", &&ip[0], port);
        spawn UV_TCP_Server(&&ip[0], port, backlog, _, &shared);

        await shared.ok;
        await shared.ok;
        [[
            CHAIN2 = GG.chain_flatten('||0|')
            print'===> SERVER'
            print('>'..string.rep('=',78))
            print(GG.chain_tostring('||0|'))
            print('<'..string.rep('=',78))
        ]];
        [=[
            assert( #CHAIN2==4 and
                    CHAIN2[4].txs[1]==[[0.3]])
            print'OK 2/2!'
        ]=];
    end

end

escape 0;
