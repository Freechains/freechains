#include "common.ceu"

///////////////////////////////////////////////////////////////////////////////
// 1<-0->2
//  - close the connection "1" during last message
//      - 0.3 is not received by "1"
///////////////////////////////////////////////////////////////////////////////

par/and do

///////////////////////////////////////////////////////////////////////////////
// 0
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        var UV_TCP_Server_Data shared = _;
        [[
            dofile 'src/common.lua'
            CFG = {
                chains = {
                    zeros_raise = TODO,     -- global and per-chain
                    [1] = {
                        key   = '',
                        zeros = 0,
                        peers = {
                            { address='127.0.0.1', port=8331 },
                            { address='127.0.0.1', port=8332 },
                        },
                    },
                },
            }
        ]];

        await Init();
        await 100ms;

        native/plain _uv_stream_t_ptr;
        native/pre do
            typedef uv_stream_t* uv_stream_t_ptr;
        end
        code/await Disconnect (var _uv_stream_t_ptr h) -> none do
            await async (h) do
                emit UV_STREAM_ERROR(h,-_ECONNRESET);
            end
        end
        pool[1] Disconnect ds;

        var int i;
        loop i in [1 -> 3] do
            [[
                ARGS = {
                    message = {
                        version = '1.0',
                        chain = {
                            key   = '',
                            zeros = 0,
                        },
                        payload = '0.'..@i
                    }
                }
            ]];
            var&? Client c = spawn Client(&shared);

            // close 1st connection for 3rd message
            if i == 3 then
                // 2nd message
                var int j;
                loop j in [1 -> 1] do
                    // 4 connections
                    //  client -> server = 2
                    //  client -> server = 2
                    var _uv_connect_t&& c;
                    var int err;
                    (c,err) = await UV_STREAM_CONNECT;
                    if j == 1 then
                        spawn Disconnect(_ID(c:handle)) in ds;
                    end
                end
            end
            await c;
        end
    end

with

///////////////////////////////////////////////////////////////////////////////
// 1
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        var UV_TCP_Server_Data shared = _;
        [[
            dofile 'src/common.lua'
            CFG = {
                server = {
                    address = '127.0.0.1',
                    port    = '8331',
                    backlog = 128,
                },
                chains = {
                    zeros_raise = TODO,     -- global and per-chain
                    [1] = {
                        key   = '',
                        zeros = 0,
                    },
                },
            }
        ]];

        await Init();

        var[] byte ip      = [] .. [[ CFG.server.address ]] .. [0];
        var   int  port    = [[ CFG.server.port ]];
        var   int  backlog = [[ CFG.server.backlog ]];
        _dbg(0, "server | listening in %s:%d", &&ip[0], port);
        spawn UV_TCP_Server(&&ip[0], port, backlog, _, &shared);

        await shared.ok;
        await shared.ok;
        [[
            CHAIN1 = FC.chain_flatten('||0|')
            print'===> SERVER'
            print('>'..string.rep('=',78))
            print(FC.chain_tostring('||0|'))
            print('<'..string.rep('=',78))
        ]];
        [=[
            assert( #CHAIN1==3 and
                    CHAIN1[3].pub.payload==[[0.2]])
            print'OK 1/2!'
        ]=];
    end

with

///////////////////////////////////////////////////////////////////////////////
// 2
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        var UV_TCP_Server_Data shared = _;
        [[
            dofile 'src/common.lua'
            CFG = {
                server = {
                    address = '127.0.0.1',
                    port    = '8332',
                    backlog = 128,
                },
                chains = {
                    [1] = {
                        key   = '',
                        zeros = 0,
                    },
                },
            }
        ]];

        await Init();

        var[] byte ip      = [] .. [[ CFG.server.address ]] .. [0];
        var   int  port    = [[ CFG.server.port ]];
        var   int  backlog = [[ CFG.server.backlog ]];
        _dbg(0, "server | listening in %s:%d", &&ip[0], port);
        spawn UV_TCP_Server(&&ip[0], port, backlog, _, &shared);

        await shared.ok;
        await shared.ok;
        await shared.ok;
        [[
            CHAIN2 = FC.chain_flatten('||0|')
            print'===> SERVER'
            print('>'..string.rep('=',78))
            print(FC.chain_tostring('||0|'))
            print('<'..string.rep('=',78))
        ]];
        [=[
            assert( #CHAIN2==4 and
                    CHAIN2[4].pub.payload==[[0.3]])
            print'OK 2/2!'
        ]=];
    end

end

escape 0;
