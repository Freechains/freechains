#define DETERMINISTIC
#include "common.ceu"

///////////////////////////////////////////////////////////////////////////////
// 0->1
///////////////////////////////////////////////////////////////////////////////

var[] byte chain0;
var[] byte chain1;

var int chain0_len = _;
var int chain1_len = _;

par/and do

///////////////////////////////////////////////////////////////////////////////
// 0
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        [[
            dofile 'src/common.lua'
            CFG = {
                no_persistency = true,
                chains = {
                    [''] = {
                        key   = '',
                        zeros = 0,
                        peers = {
                            { address='127.0.0.1', port=8341 },
                        },
                    },
                },
            }
        ]]

        var& Init init = spawn Init();
        await 100ms;

        [[
            ARGS = {
                message = {
                    version = '1.0',
                    chain = {
                        key   = '',
                        zeros = 0,
                    },
                    payload = '1.1',
                }
            }
        ]]
        await Client(&init.shared);
        await 100ms;

        [[
            ARGS = {
                message = {
                    version = '1.0',
                    chain = {
                        key   = '',
                        zeros = 0,
                    },
                    payload = '1.2',
                }
            }
        ]];
        await Client(&init.shared);

        [[
            chain0 = FC.chain_flatten('||0|')
            print('===> 0, #'..#chain0)
            print('>'..string.rep('=',78))
            print(FC.tostring(chain0))
            print('<'..string.rep('=',78))
        ]];
        chain0 = []..[[FC.tostring(chain0)]];
        chain0_len = [[#chain0]];
    end
with

///////////////////////////////////////////////////////////////////////////////
// 1
///////////////////////////////////////////////////////////////////////////////

    lua[] do
        [[
            dofile 'src/common.lua'
            CFG = {
                daemon = {
                    address = '127.0.0.1',
                    port    = '8341',
                    backlog = 128,
                },
                no_persistency = true,
                chains = {
                    [''] = {
                        key   = '',
                        zeros = 0,
                    },
                },
            }
        ]]

        var& Init init = spawn Init();
        AWAIT_CHAIN_LENGTH(init.shared, '', 0, 1);

        par/and do
            AWAIT_CHAIN_LENGTH(init.shared, '', 0, 4);
        with
            [[
                ARGS = {
                    message = {
                        version = '1.0',
                        chain = {
                            key   = '',
                            zeros = 0,
                        },
                        payload = '2.1',
                    }
                }
            ]];
            await Client(&init.shared);
            await 200ms;
            [[
                ARGS = {
                    message = {
                        version = '1.0',
                        chain = {
                            key   = '',
                            zeros = 0,
                        },
                        payload = '2.2',
                    }
                }
            ]];
            await Client(&init.shared);
        end

        [[
            chain1 = FC.chain_flatten('||0|')
            print('===> 1, #'..#chain1)
            print('>'..string.rep('=',78))
            print(FC.tostring(chain1))
            print('<'..string.rep('=',78))
            assert(chain1[4] and chain1[4].pub.payload == '2.1', #chain1)
            assert(chain1[5] and chain1[5].pub.payload == '2.2', #chain1)
        ]];
        chain1 = []..[[FC.tostring(chain1)]];
        chain1_len = [[#chain1]];
    end

end

[[
    assert(@chain0_len==3 and @chain1_len==5)
    print'OK!'
]]

escape 0;
